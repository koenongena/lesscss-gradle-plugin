apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'org.lesscss'
artifactId = 'lesscss-gradle-plugin'
version = '0.1'

description = 'A gradle plugin used to compile less css files.'

manifest.mainAttributes('Implementation-Title': "${project.name}",
                        'Implementation-Version': project.version,
                        'Built-By': System.properties['user.name'])

configurations {
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom integrationTestCompile, testRuntime
    }
}

sourceSets {
    integrationTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integrationTestCompile
        runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
    }
}

repositories {
    mavenCentral()
}


sourceCompatability = JavaVersion.VERSION_1_6
targetCompatability = JavaVersion.VERSION_1_6

dependencies {
    compile 'org.lesscss:lesscss:1.3.0'
    groovy localGroovy()
    compile gradleApi()
    testCompile(
            [group: 'junit', name: 'junit', version: '4.8.2']
    )
}


task integrationTest(type: Test, dependsOn: jar) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperties['integTest.projects'] = new File(sourceSets.integrationTest.output.resourcesDir, 'projects').absolutePath
}

//check.dependsOn integrationTest

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives groovydocJar
}

idea {
    module {
        testSourceDirs += file('src/integTest/groovy')
        downloadJavadoc = false
        downloadSources = false
    }
}

task generatePom << {
    def generatedPomFileName = 'pom.xml'

    pom {
        project(pomConfiguration)
    }.writeTo("$buildDir/$generatedPomFileName")
}

def getPomConfiguration() {
    return {
        groupId project.group
        artifactId project.artifact
        version project.version
        packaging 'jar'
        name 'Gradle Lesscss Plugin'
        description project.description
        url 'https://github.com/koenongena/gradle-lesscss-plugin'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/koenongena/gradle-lesscss-plugin'
            connection 'scm:git://github.com/koenongena/gradle-lesscss-plugin.git'
            developerConnection 'scm:git@github.com:koenongena/gradle-lesscss-plugin.git'
        }
        developers {
            developer {
                id 'koenongena'
                name 'Koen Ongena'
            }

        }
    }
}

